model TomMacBase

uses "mmxprs"

! Declarations -----------------------------------------------------------------------------------

declarations

  ! Decision Variables
  Products: set of string
  Varieties: set of string
  Contents: set of string
  PeriodNames: set of string


  ! Time Variables
  NPeriods = 4
  PeriodNumbers = 1..NPeriods


  ! Constraints
  WarehouseSize: real
  TomatoSupply: array(Varieties, PeriodNumbers) of real
  TomatoCost: array(Varieties, PeriodNumbers) of real
  Demand: array(Products) of real
  ProductionLimit: array(Products) of real
  Contract: array(Products) of real


  ! Objective Function Quantities
  StorageCost: real
  SalePrice: array(Products) of real
  ContractPrice: array(Products) of real


  ! Further Information
  ProductReduction: array(Products) of real
  OtherIngredientFactor: array(Products) of real
  OtherIngredientCost: array(Products) of real
  ProductionCost: array(Products) of real ! Other Ingredient production cost

  TomatoProperties: array(Contents, Varieties) of real
  ProductProperties: array(Contents, Products) of real


	! Further Consultation
	NewWarehouseSize: real
	NewWarehouseCost: real

	NewProductionIncrease: real
	NewProductionCost: real

	NewContract: array(Products) of real
	NewContractPrice: array(Products) of real


  ! Product Optimising Quantities
	Make: array(Products, PeriodNumbers) of mpvar
	Sell: array(Products, PeriodNumbers) of mpvar
	Stock: array(Products, PeriodNumbers) of mpvar
	Store: array(Products, PeriodNumbers) of mpvar
	
	
	! Material Optimising Quantities
	VarietyUsed: array(Varieties, PeriodNumbers) of mpvar
	
	
	! Mix Optimising Quantities
	MixProduced: array(Products, PeriodNumbers) of mpvar
	

end-declarations

! Initialisations --------------------------------------------------------------------------------

initialisations from "TomMac.dat"

  Products Varieties Contents
  ProductReduction OtherIngredientFactor OtherIngredientCost
  ProductionCost TomatoProperties ProductProperties
  StorageCost WarehouseSize

  NewWarehouseSize NewProductionCost
  NewProductionIncrease NewWarehouseCost

end-initialisations

initialisations from "TomMacQuarters.dat"

  PeriodNames TomatoSupply TomatoCost
  Demand SalePrice
  Contract ContractPrice
  ProductionLimit

  NewContract NewContractPrice

end-initialisations


! Constraints ------------------------------------------------------------------------------------

! Contracts
forall(p in Products, m in PeriodNumbers) do
  ContractCS(p,m) := Sell(p,m) >= Contract(p)
end-do

! Demand
forall(p in Products, m in PeriodNumbers) do
  DemandCS(p,m) := Sell(p,m) <= Contract(p) + Demand(p)
end-do

! Manufacture
forall(p in Products, m in PeriodNumbers) do
  ManufactureCS(p,m) := Make(p,m) <= ProductionLimit(p)
end-do

! Availability
forall(v in Varieties, m in PeriodNumbers) do 
  AvailabilityCS(v,m) := VarietyUsed(v,m) <= TomatoSupply(v,m)
end-do 

! Storage 
forall(m in PeriodNumbers) do
  sum(p in Products) Stock(p,m) <= WarehouseSize
end-do


! Establishing Relationship between VarietyUsed and MixProduced
forall(m in PeriodNumbers, p in Products, c in Contents) do
  MixCS(m,p,c) := sum(v in Varieties) TomatoProperties(c,v) * VarietyUsed(v,m) =
  MixProduced(p,m) * ProductProperties(c,p)
end-do

! Establishing Relationship between MixProduced and Make
forall(p in Products, m in PeriodNumbers) do
  VarietyUsedCS(p,m) := Make(p,m) = 
  (1 + OtherIngredientFactor(p)) * 
  ProductReduction(p) * MixProduced(p,m)
end-do

! Establishing Relationship between Store, Stock, Make and Sell
forall(p in Products, m in PeriodNumbers) do
	if m = 1 then
		InventoryCS(p, m) := Stock(p, m) = Stock(p, NPeriods) + Make(p, m) - Sell(p, m)
		StorageCS(p, m) := Store(p, m) = Stock(p, NPeriods) + Make(p, m)
	else
		InventoryCS(p, m) := Stock(p, m) = Stock(p, m-1)     + Make(p, m) - Sell(p, m)
		StorageCS(p, m) := Store(p, m) = Stock(p, m-1) + Make(p, m)
	end-if
end-do

! Objective Function ----------------------------------------------------------------------------

! Profits
ContractProfit := NPeriods * sum(p in Products)
Contract(p) * ContractPrice(p)

DemandProfit := sum(p in Products, m in PeriodNumbers)
(Make(p,m) - Contract(p)) * SalePrice(p)

TotalProfit := ContractProfit + DemandProfit


! Costs
TotalMaterialCost := sum(v in Varieties, m in PeriodNumbers) 
TomatoCost(v,m) * VarietyUsed(v,m)

OtherIngredCost := sum(p in Products, m in PeriodNumbers) 
ProductReduction(p) * OtherIngredientFactor(p) * MixProduced(p,m) * OtherIngredientCost(p)

ProdCost := sum(p in Products, m in PeriodNumbers)
MixProduced(p,m) * ProductionCost(p)

TotalCost := TotalMaterialCost + OtherIngredCost + ProdCost


! Objective Function
TotalIncome := TotalProfit - TotalCost

maximise(TotalIncome)


! Print Locally ---------------------------------------------------------------------------------

writeln
write("The base level profit is Â£", strfmt(getsol(TotalIncome),6))


! Print to CSV ----------------------------------------------------------------------------------


end-model
